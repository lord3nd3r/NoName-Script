; 
; NoNameScript-v2 v5.0 - system information - coded by greeny & mute
; Updated for robust WMI handling on Wine/Linux
; 

alias sysinfo { 
  if ($1 == -d) { sysconfig }
  else { psys all }
}

alias sysconfig { nndlg -mh sysinfo }

dialog sysinfo.load {
  title "Loading system information..."
  size -1 -1 254 76
  option pixels
  text "", 1, 4 8 244 22, center
  text "", 2, 4 54 244 16, center
  text "", 3, 4 32 244 16, center
}

on *:dialog:sysinfo.load:init:0:{
  if ($mdxfile(ctl_gen)) { mdx SetControlMDX $dname 3 ProgressBar > $v1 }
  mdx SetFont $dname 1 16 700 Tahoma
}

dialog sysinfo {
  title "System information [/sysinfo]"
  size -1 -1 396 304
  option pixels
  icon scripts\pics\nntray.ico
  list 3, 4 4 388 20, size
  list 1, 4 30 388 218, size
  box "", 4, -2 262 402 8
  button "&Refresh", 6, 4 276 80 24
  button "&Say to active", 5, 88 276 80 24
  button "S&ay overview", 10, 168 276 80 24
  button "&Close", 2, 312 276 80 24, ok
  text "", 7, 4 122 386 30, center
  text "", 8, 6 250 258 16
  text "", 9, 268 250 122 16, right
}

on *:dialog:sysinfo:*:*:{
  if ($devent == init) {
    nndlg -m sysinfo.load
    did -a sysinfo.load 1 Loading system information...
    sysinfo.lref 1 Connecting to WMI... (this may take a while)
    if ($mdxfile(views)) { mdx SetControlMDX $dname 1 ListView report showsel single rowselect > $v1 }
    
    ; Setup columns (native listview fallback if MDX fails)
    ; did -i $dname 1 1 headerdims 80:1 100:2 180:3
    ; did -i $dname 1 1 headertext + 0 Hardware	+ 0 Information	+ 0 Value
    
    mdx SetFont $dname 7 16 700 Tahoma
    if ($mdxfile(bars)) { mdx SetControlMDX $dname 3 ToolBar flat list nodivider > $v1 }
    mdx SetBorderStyle $dname 3
    ; did -i $dname 3 1 bmpsize 16 16
    
    did $iif($nomsg,-b,-e) sysinfo 5,10
    
    ; Populate Toolbar/Tabs
    var %z = 1
    var %items = CPUs|Motherboard|RAM|Logical drives|Harddisks|Video controllers|Sound cards|Network adapters|Printers|Battery|Windows information
    var %icons = chip|circuit|ram chip|select|drive|monitor|sound|hardware card|printer|component|windows
    
    while ($gettok(%icons, %z, 124)) {
      var %icon = $v1
      var %label = $gettok(%items, %z, 124)
      ; Note: MDX setimage calls removed for stability/compatibility
      did -a $dname 3 %label
      inc %z
    }
    
    set %sysinfo.curpage 2
    did -c $dname 3 2
    sysinfo.ref Win32_Processor 2
    if ($dialog(sysinfo.load)) { dialog -c $v1 }
  }
  elseif ($devent == sclick) {
    if ($did == 3) {
      if ($did($did).sel) {
        set %sysinfo.curpage $did($did).sel
      }
      sysinfo.refb
      did -h sysinfo 3
      did -v sysinfo 3
    }
    elseif ($did == 5) { sysinfo.refb -s }
    elseif ($did == 6) { sysinfo.refb }
    elseif ($did == 10) { psys all }
  }
  elseif ($devent == close) { unset %sysinfo.curpage }
}

alias sysinfo.lref {
  if ($dialog(sysinfo.load)) {
    did -a sysinfo.load 2 $2-
    if ($3 isnum) did -a sysinfo.load 3 $int($calc(($1 / 3) * 100)) 100
  }
}

alias sysinfo.ref {
  var %wmi_class = $1
  var %flags = $2
  
  if ($dialog(sysinfo)) { var %t = $iif($did(sysinfo,3).seltext,$gettok($v1,2-,9),CPUs) }
  
  if (%flags != -s) && (%flags != -g) {
    var %ticks = $nnticks
    did -h sysinfo 1,8,9
    did -v sysinfo 7
    did -ra sysinfo 7 Refreshing %t $+ ... please wait!
  }
  else { unset %sysinfo.say }
  
  ; --- Safe WMI Execution ---
  if ($com(sysloc)) .comclose sysloc
  if ($com(sysserv)) .comclose sysserv
  if ($com(sysinst)) .comclose sysinst
  
  .comopen sysloc WbemScripting.SWbemLocator
  if ($comerr) || (!$com(sysloc)) { goto wmi_error }
  
  var %c = $com(sysloc,ConnectServer,3,dispatch* sysserv)
  .comclose sysloc
  if ($comerr) || (!%c) || (!$com(sysserv)) { goto wmi_error }
  
  if ($com(sysserv)) {
    var %i = $com(sysserv,InstancesOf,3,string,%wmi_class,dispatch* sysinst)
    .comclose sysserv
    if ($comerr) || (!%i) || (!$com(sysinst)) { goto wmi_error }
  }
  else { goto wmi_error }
  
  if ($com(sysinst)) {
    sysinfo.lref 2 Enumerating collection...
    noop $com(sysinst,Count,3)
    
    if ($com(sysinst).result == $null) { goto wmi_error }
    
    var %n = $com(sysinst).result
    var %m = 1
    
    sysinfo.lref 3 Getting information...
    
    if (%flags != -s) && (%flags != -g) { sysinfo.ainfo %wmi_class 0 -s }
    
    while (%m <= %n) {
      var %op = $iif(%flags == -s,sysinfo.sinfo,$iif(%flags == -g,sysinfo.ginfo,sysinfo.ainfo))
      var %meta = $iif(%flags == -s || %flags == -g,$iif(%m == 1,-s $+ $iif(%m == %n,e) ,$iif(%m == %n,-e)))
      
      ; Execute safely
      if ($isalias(%op)) %op %wmi_class %m %meta
      
      inc %m
    }
    
    if ($dialog(sysinfo)) && ($did(sysinfo,1).lines == 1) { did -a sysinfo 1 None detected. }
    
    if (%flags == -s) {
       if (%sysinfo.say) {
         say $v1
         unset %sysinfo.say
       }
       else { errdialog No hardware detected! }
    }
    
    .comclose sysinst
  }
  else { goto wmi_error }
  
  if (%flags != -s) && (%flags != -g) {
    did -h sysinfo 7
    did -ra sysinfo 8 %t refreshed in:
    did -ra sysinfo 9 $round($calc($nnticks - %ticks),2) second(s)
    did -v sysinfo 1,8,9
  }
  return

  :wmi_error
  if ($com(sysloc)) .comclose sysloc
  if ($com(sysserv)) .comclose sysserv
  if ($com(sysinst)) .comclose sysinst
  
  var %msg = Unable to retrieve system information (WMI Error).
  if ($dialog(sysinfo)) {
    did -ra sysinfo 7 %msg
    did -v sysinfo 7
  }
  else { 
    thmerror -a %msg
  }
  reseterror
  return
}

alias psys {
  unset %sysinfo.gather.*
  if ($$1 == all) {
    sysinfo.refb -g 2
    sysinfo.refb -g 3
    sysinfo.refb -g 4
    sysinfo.refb -g 7
    sysinfo.refb -g 12
    
    if ($error) return
    
    ; Check if we gathered data
    if (%sysinfo.gather.cpu || %sysinfo.gather.os) {
      set %sysinfo.gather.hdd $sbr(HDDs) $bytes($sysinfo.disk.total,3).suf $+ , $bytes($sysinfo.disk.total(free),3).suf free
      say %sysinfo.gather.cpu %sysinfo.gather.mobo %sysinfo.gather.ram %sysinfo.gather.hdd
      say %sysinfo.gather.vga %sysinfo.gather.os
    }
    else { thmerror -a An error occured while gathering system information. }
    
    unset %sysinfo.gather.*
  }
  else { 
    var %target = $replacex($1,cpu,2,processor,2,motherboard,3,mainboard,3,mobo,3,mb,3,ram,4,drives,5,drive,5,partitions,5,hdd,6,disks,6,disk,6,video,7,gfx,7,graphics,7,vga,7,sound,8,network,9,lan,9,printer,10,battery,11,usv,11,akku,11,os,12,windows,12,win,12)
    if (%target isnum) sysinfo.refb -s %target
  }
}

alias sysinfo.refb {
  var %flags = $1
  var %idx = $2
  
  if (%flags != -s) && (%flags != -g) { 
      if ($dialog(sysinfo)) { did -r sysinfo 1 } 
  }
  elseif ($nomsg) {
    if ($dialog(sysinfo)) errdialog you can only do this in channels and queries!
    else thmecho -a you can only do this in channels and queries!
    return
  }
  
  var %d = $iif(%idx, %idx, $iif($did(sysinfo,3).sel, $v1, 2))
  var %cls
  
  if (%d == 2) %cls = Win32_Processor
  elseif (%d == 3) %cls = Win32_BaseBoard
  elseif (%d == 4) %cls = Win32_PhysicalMemory
  elseif (%d == 5) {
     %cls = Win32_LogicalDisk
     if (%flags != -g) && (%flags != -s) {
        did -a sysinfo 1 1 HD drives	Total space	$bytes($sysinfo.disk.total(total),3).suf
        did -a sysinfo 1 1 	Free space	$bytes($sysinfo.disk.total(free),3).suf
     }
  }
  elseif (%d == 6) %cls = Win32_DiskDrive
  elseif (%d == 7) %cls = Win32_VideoController
  elseif (%d == 8) %cls = Win32_SoundDevice
  elseif (%d == 9) %cls = Win32_NetworkAdapter
  elseif (%d == 10) %cls = Win32_Printer
  elseif (%d == 11) %cls = Win32_Battery
  elseif (%d == 12) %cls = Win32_OperatingSystem
  
  if (%cls) sysinfo.ref %cls %flags
}

alias sysinfo.ainfo {
  var %cls = $1, %id = $2
  if (%cls == Win32_Processor) {
    if ($3 == -s) { }
    else {
      did -a sysinfo 1 1 CPU %id $+ 	Name	$brand($comval(sysinst,%id,Name))
      if ($comval(sysinst,%id,Caption)) { did -a sysinfo 1 1 	Family	$v1 }
      if ($comval(sysinst,%id,Manufacturer)) { did -a sysinfo 1 1 	Brand	$v1 }
      if ($comval(sysinst,%id,CurrentClockSpeed)) { did -a sysinfo 1 1 	Clock speed	$v1 $+ MHz }
      if ($comval(sysinst,%id,ExtClock)) { did -a sysinfo 1 1 	Front side bus	$v1 $+ MHz }
      if ($comval(sysinst,%id,L2CacheSize)) { did -a sysinfo 1 1 	L2 cache size	$v1 $+ KB }
      if ($comval(sysinst,%id,AddressWidth)) { did -a sysinfo 1 1 	Address width	$v1 $+ bit }
      if ($comval(sysinst,%id,SocketDesignation)) { did -a sysinfo 1 1 	Socket type	$v1 }
      if ($comval(sysinst,%id,ProcessorId)) { did -a sysinfo 1 1 	Processor ID	$v1 }
      if ($comval(sysinst,%id,LoadPercentage) != $null) { did -a sysinfo 1 1 	Current load	$v1 $+ % }
    }
  }
  elseif (%cls == Win32_PhysicalMemory) {
    if ($3 == -s) { }
    else {
      did -a sysinfo 1 1 Module %id $+ 	Type	$comval(sysinst,%id,Name)
      if ($comval(sysinst,%id,Capacity)) { did -a sysinfo 1 1 	Capacity	$bytes($v1).suf }
      if ($comval(sysinst,%id,BankLabel)) { did -a sysinfo 1 1 	Bank label	$v1 }
      if ($comval(sysinst,%id,FormFactor)) { did -a sysinfo 1 1 	Form factor	$replacex($v1,10,PGA,11,RIMM,12,SODIMM,13,SRIMM,14,SMD,15,SSMP,16,QFP,17,TQFP,18,SOIC,19,LCC,20,PLCC,21,BGA,22,FPBGA,23,LGA,0,Unknown,1,Other,2,SIP,3,DIP,4,ZIP,5,SOJ,6,Proprietary,7,SIMM,8,DIMM,9,TSOP) }
      if ($comval(sysinst,%id,MemoryType)) { did -a sysinfo 1 1 	Memory type	$replacex($v1,10,ROM,11,Flash,12,EEPROM,13,FEPROM,14,EPROM,15,CDRAM,16,3DRAM,17,SDRAM,18,SGRAM,19,RDRAM,20,DDR,0,Unknown,1,Other,2,DRAM,3,Synchronous DRAM,4,Cache DRAM,5,EDO,6,EDRAM,7,VRAM,8,SRAM,9,RAM) }
    }
  }
  elseif (%cls == Win32_LogicalDisk) {
    if ($3 == -s) { }
    else {
      var %x = $comval(sysinst,%id,VolumeSerialNumber)
      did -a sysinfo 1 1 $comval(sysinst,%id,Name) $iif(!%x,$nbr(empty)) $+ 	Type	$comval(sysinst,%id,Description) $iif($comval(sysinst,%id,FileSystem),$nbr($v1))
      if ($comval(sysinst,%id,VolumeName) != $null) { did -a sysinfo 1 1 	Label	$v1 }
      if ($comval(sysinst,%id,Size) != $null) { did -a sysinfo 1 1 	Size	$bytes($v1).suf $nbr($bytes($comval(sysinst,%id,FreeSpace)).suf free) }
      if (%x) { did -a sysinfo 1 1 	Serial number	$v1 }
      else { did -a sysinfo 1 1 	Status	No media inserted! }
      if ($comval(sysinst,%id,SystemName)) { did -a sysinfo 1 1 	Owner	$v1 }
    }
  }
  elseif (%cls == Win32_DiskDrive) {
    if ($3 == -s) { }
    else {
      did -a sysinfo 1 1 Drive %id $+ 	Name	$comval(sysinst,%id,Model)
      if ($comval(sysinst,%id,MediaType)) { did -a sysinfo 1 1 	Type	$v1 }
      if ($comval(sysinst,%id,Size)) { did -a sysinfo 1 1 	Size	$bytes($v1).suf }
      if ($comval(sysinst,%id,Partitions)) { did -a sysinfo 1 1 	Partitions	$v1 }
      if ($comval(sysinst,%id,InterfaceType)) { did -a sysinfo 1 1 	Interface	$v1 }
      if ($comval(sysinst,%id,TotalSectors)) { did -a sysinfo 1 1 	Sectors	$bytes($v1,b) }
      if ($comval(sysinst,%id,BytesPerSector)) { did -a sysinfo 1 1 	Bytes/sector	$bytes($v1,b) }
      if ($comval(sysinst,%id,TotalTracks)) { did -a sysinfo 1 1 	Tracks	$bytes($v1,b) }
      if ($comval(sysinst,%id,SectorsPerTrack)) { did -a sysinfo 1 1 	Sectors/track	$bytes($v1,b) }
      if ($comval(sysinst,%id,TotalCylinders)) { did -a sysinfo 1 1 	Cylinders	$bytes($v1,b) }
      if ($comval(sysinst,%id,TracksPerCylinder)) { did -a sysinfo 1 1 	Tracks/cylinder	$bytes($v1,b) }
      if ($comval(sysinst,%id,TotalHeads)) { did -a sysinfo 1 1 	Heads	$bytes($v1,b) }
    }
  }
  elseif (%cls == Win32_VideoController) {
    if ($3 == -s) { }
    else {
      did -a sysinfo 1 1 Video controller %id $+ 	Name	$comval(sysinst,%id,Caption)
      if ($comval(sysinst,%id,VideoProcessor)) { did -a sysinfo 1 1 	Processor	$v1 }
      if ($comval(sysinst,%id,AdapterRAM)) { did -a sysinfo 1 1 	Video RAM	$bytes($v1).suf }
      if ($comval(sysinst,%id,VideoMemoryType) isnum 3-13) { did -a sysinfo 1 1 	Video RAM type	$replacex($v1,3,VRAM,4,DRAM,5,SRAM,6,WRAM,7,EDO RAM,8,Burst Synchronous DRAM,9,Pipelined Burst SRAM,10,CDRAM,11,3DRAM,12,SDRAM,13,SGRAM) }
      if ($comval(sysinst,%id,VideoArchitecture) isnum 1-12) { did -a sysinfo 1 1 	Video architecture	$replacex($v1,10,8514A,11,XGA,12,Linear Frame Buffer,1,Other,2,Unknown,3,CGA,4,EGA,5,VGA,6,SVGA,7,MDA,8,HGC,9,MCGA) }
      if ($comval(sysinst,%id,AdapterDACType)) { did -a sysinfo 1 1 	DAC type	$v1 }
      var %h = $comval(sysinst,%id,CurrentHorizontalResolution),%v = $comval(sysinst,%id,CurrentVerticalResolution)
      if (%h isnum 0-) && (%v isnum 0-) { did -a sysinfo 1 1 	Resolution	$+(%h,x,%v) }
      if ($comval(sysinst,%id,CurrentBitsPerPixel)) { did -a sysinfo 1 1 	Color depth	$v1 $+ bpp }
      if ($comval(sysinst,%id,CurrentRefreshRate) isnum 0-) { did -a sysinfo 1 1 	Refresh rate	$v1 $+ Hz }
      if ($comval(sysinst,%id,CurrentScanMode) isnum 3-4) { did -a sysinfo 1 1 	Scan mode	$replacex($v1,3,Interlaced,4,Non Interlaced) }
      if ($comval(sysinst,%id,InstalledDisplayDrivers)) { did -a sysinfo 1 1 	Display driver	$v1 }
      if ($comval(sysinst,%id,DriverVersion)) { did -a sysinfo 1 1 	Driver version	$v1 }
      if ($comval(sysinst,%id,DriverDate)) { did -a sysinfo 1 1 	Driver date	$wtfconv($v1).both }
    }
  }
  elseif (%cls == Win32_NetworkAdapter) {
    if ($3 == -s) { }
    else {
      did -a sysinfo 1 1 Adapter %id $+ 	Name	$comval(sysinst,%id,Description)
      if ($comval(sysinst,%id,Manufacturer)) { did -a sysinfo 1 1 	Brand	$v1 }
      if ($comval(sysinst,%id,MACAddress)) { did -a sysinfo 1 1 	MAC address	$v1 }
      if ($comval(sysinst,%id,ServiceName)) { did -a sysinfo 1 1 	Service name	$v1 }
      if ($comval(sysinst,%id,TimeOfLastReset)) { did -a sysinfo 1 1 	Last reset	$wtfconv($v1).both }
    }
  }
  elseif (%cls == Win32_OperatingSystem) {
    if ($3 == -s) { }
    else {
      did -a sysinfo 1 1 Operating system	Name	$sysinfo.fixosdisp($comval(sysinst,%id,Caption))
      if ($comval(sysinst,%id,Version)) { did -a sysinfo 1 1 	Version	$v1 }
      if ($comval(sysinst,%id,ServicePackMajorVersion)) { did -a sysinfo 1 1 	Service pack version	$v1 }
      if ($comval(sysinst,%id,BuildNumber)) { did -a sysinfo 1 1 	Build number	$v1 }
      if ($comval(sysinst,%id,BuildType)) { did -a sysinfo 1 1 	Build type	$v1 }
      if ($comval(sysinst,%id,NumberOfProcesses)) { did -a sysinfo 1 1 	Processes	$v1 }
      if ($comval(sysinst,%id,FreePhysicalMemory)) { did -a sysinfo 1 1 	Free RAM	$bytes($calc($v1 *1024)).suf }
      if ($comval(sysinst,%id,FreeVirtualMemory)) { did -a sysinfo 1 1 	Free virtual memory	$bytes($calc($v1 *1024)).suf }
      if ($comval(sysinst,%id,TotalVirtualMemorySize)) { did -a sysinfo 1 1 	Total virtual memory size	$bytes($calc($v1 *1024)).suf }
      if ($comval(sysinst,%id,InstallDate)) { did -a sysinfo 1 1 	First installed	$wtfconv($v1).both }
      did -a sysinfo 1 1 	Uptime	$sduration($uptime(system,3))
      did -a sysinfo 1 1 	Uptime record	$sduration($sysinfo.uptimerecord)
      if ($comval(sysinst,%id,BootDevice)) { did -a sysinfo 1 1 	Boot device	$v1 }
      if ($comval(sysinst,%id,RegisteredUser)) { did -a sysinfo 1 1 	Registered user	$v1 }
    }
  }
  elseif (%cls == Win32_SoundDevice) {
    if ($3 == -s) { }
    else {
      did -a sysinfo 1 1 Sound card %id $+ 	Name	$comval(sysinst,%id,Caption)
      if ($comval(sysinst,%id,Manufacturer)) { did -a sysinfo 1 1 	Brand	$v1 }
      if ($comval(sysinst,%id,DeviceID)) { did -a sysinfo 1 1 	DeviceID	$v1 }
    }
  }
  elseif (%cls == Win32_Printer) {
    if ($3 == -s) { }
    else {
      did -a sysinfo 1 1 Printer %id $+ 	Name	$comval(sysinst,%id,Caption)
      if ($comval(sysinst,%id,PortName)) { did -a sysinfo 1 1 	Port	$v1 }
      did -a sysinfo 1 1 	Status	$replacex($comval(sysinst,%id,PrinterStatus),1,Other,2,Unknown,3,Idle,4,Printing,5,Warmup,6,Stopped printing,7,Offline)
      var %h = $comval(sysinst,%id,HorizontalResolution),%v = $comval(sysinst,%id,VerticalResolution)
      if (%h isnum 0-) && (%v isnum 0-) { did -a sysinfo 1 1 	Resolution	$+(%h,x,%v,dpi) }
      if ($comval(sysinst,%id,PrintProcessor)) { did -a sysinfo 1 1 	Print spooler	$v1 }
    }
  }
  elseif (%cls == Win32_BaseBoard) {
    if ($3 == -s) { }
    else {
      var %n = $comval(sysinst,%id,Manufacturer),%c = $comval(sysinst,%id,Version)
      did -a sysinfo 1 1 Motherboard	Brand	$iif(!%n || %n = null,Unknown,%n)
      if ($comval(sysinst,%id,Product)) { did -a sysinfo 1 1 	Product	$v1 }
      if (%c) && (%c != null) && (%c != none) { did -a sysinfo 1 1 	Version	$v1 }
    }
  }
  elseif (%cls == Win32_Battery) {
    if ($3 == -s) { }
    else {
      did -a sysinfo 1 1 Battery %id $+ 	Caption	$comval(sysinst,%id,Caption)
      if ($comval(sysinst,%id,DeviceID)) { did -a sysinfo 1 1 	Name	$v1 }
      if ($comval(sysinst,%id,EstimatedRunTime) < 9999) {
        did -a sysinfo 1 1 	Run time remaining	$v1 minutes
        did -a sysinfo 1 1 	Status	Running on battery
      }
      else { did -a sysinfo 1 1 	Status	Charging/AC }
      if ($comval(sysinst,%id,EstimatedChargeRemaining) isnum 0-100) { did -a sysinfo 1 1 	Charge remaining	$v1 $+ % }
    }
  }
}

alias sysinfo.fixosdisp { return $replace($1-,™,,®,) }

alias sysinfo.ginfo {
  var %cls = $1, %id = $2
  if (%cls == Win32_Processor) { set %sysinfo.gather.cpu $sbr(CPU $+ %id) $brand($comval(sysinst,%id,Name)) @ $comval(sysinst,%id,CurrentClockSpeed) $+ MHz }
  elseif (%cls == Win32_BaseBoard) {
    var %m = $comval(sysinst,%id,Manufacturer),%b = $comval(sysinst,%id,Product)
    set %sysinfo.gather.mobo $iif(%m || %b,$nbr(%m %b mainboard))
  }
  elseif (%cls == Win32_PhysicalMemory) {
    if (s isin $3) { set %sysinfo.gram 0 }
    inc %sysinfo.gram $comval(sysinst,%id,Capacity)
    if (e isin $3) {
      set %sysinfo.gather.ram $iif(%sysinfo.gram,$sbr(RAM) $bytes(%sysinfo.gram).suf)
      unset %sysinfo.gram
    }
  }
  elseif (%cls == Win32_VideoController) { set %sysinfo.gather.vga $sbr(VGA $+ %id) $comval(sysinst,%id,Caption) $iif($comval(sysinst,%id,AdapterRAM),$nbr($bytes($v1).suf)) $+ $iif($comval(sysinst,%id,CurrentHorizontalResolution),$+($chr(44) $v1,x,$comval(sysinst,%id,CurrentVerticalResolution),x,$comval(sysinst,%id,CurrentBitsPerPixel))) $+ $iif($comval(sysinst,%id,CurrentRefreshRate) isnum 0-,$chr(44) $v1 $+ Hz) }
  elseif (%cls == Win32_OperatingSystem) {
    set %sysinfo.gather.os $sbr(OS) $sysinfo.fixosdisp($comval(sysinst,%id,Caption)) $+ $iif($comval(sysinst,%id,ServicePackMajorVersion),$chr(32) $nbr(SP $+ $v1)) $+ , $sduration($uptime(system,3)) uptime, $sduration($sysinfo.uptimerecord) uptime record
    set %sysinfo.gather.ram %sysinfo.gather.ram $+ , $bytes($calc($comval(sysinst,%id,FreePhysicalMemory) *1024)).suf free
  }
}

alias sysinfo.sinfo {
  var %cls = $1, %id = $2
  var %n
  if (%cls == Win32_Processor) { var %c = $comval(sysinst,%id,L2CacheSize),%s = $comval(sysinst,%id,SocketDesignation),%n = $sbr(CPU %id) $brand($comval(sysinst,%id,Name)) $nbr($iif(%c,%c $+ KB L2 Cache) $+ $iif(%s,$iif(%c,$chr(44)) %s)) @ $comval(sysinst,%id,CurrentClockSpeed) $+ MHz $+ $iif($comval(sysinst,%id,ExtClock),$chr(32) $nbr($v1 $+ MHz FSB)) $+ $iif($comval(sysinst,%id,LoadPercentage) isnum,$chr(44) $v1 $+ % load) }
  elseif (%cls == Win32_BaseBoard) { var %v = $comval(sysinst,%id,Version),%n = $sbr(Motherboard) $comval(sysinst,%id,Manufacturer) $comval(sysinst,%id,Product) $+ $iif(%v && %v != null && %v != none,$chr(44) version $v1) }
  elseif (%cls == Win32_Battery) { var %z = $comval(sysinst,%id,EstimatedRunTime),%n = $sbr(Battery %id) $comval(sysinst,%id,Caption) $+ : $comval(sysinst,%id,DeviceID) $+ $iif(%z < 9999,$chr(44) status: running on battery $+ $chr(44) run time remaining: %z minutes,$chr(44) status: charging/AC) $+ , current charge: $comval(sysinst,%id,EstimatedChargeRemaining) $+ % }
  elseif (%cls == Win32_PhysicalMemory) { var %n = $sbr(RAM module %id) $bytes($comval(sysinst,%id,Capacity)).suf $+ $iif($comval(sysinst,%id,FormFactor),$chr(44) form factor: $replacex($v1,10,PGA,11,RIMM,12,SODIMM,13,SRIMM,14,SMD,15,SSMP,16,QFP,17,TQFP,18,SOIC,19,LCC,20,PLCC,21,BGA,22,FPBGA,23,LGA,0,Unknown,1,Other,2,SIP,3,DIP,4,ZIP,5,SOJ,6,Proprietary,7,SIMM,8,DIMM,9,TSOP)) $+ $iif($comval(sysinst,%id,MemoryType),$chr(44) type: $replacex($v1,10,ROM,11,Flash,12,EEPROM,13,FEPROM,14,EPROM,15,CDRAM,16,3DRAM,17,SDRAM,18,SGRAM,19,RDRAM,20,DDR,1,Other,2,DRAM,3,Synchronous DRAM,4,Cache DRAM,5,EDO,6,EDRAM,7,VRAM,8,SRAM,9,RAM)) }
  elseif (%cls == Win32_LogicalDisk) { var %s = $comval(sysinst,%id,Size),%f = $comval(sysinst,%id,FreeSpace),%n = $sbr($comval(sysinst,%id,Name)) $comval(sysinst,%id,Description) $nbr($iif(%s != $null && %f != $null,Free: $+($bytes(%f).suf,$nbr($percent(%f,%s,1)),/,$bytes(%s).suf,$chr(44) $comval(sysinst,%id,FileSystem),$chr(44) label: $iif($comval(sysinst,%id,VolumeName),$shorten(30,$v1),unknown)) ,No disc)) }
  elseif (%cls == Win32_DiskDrive) { var %n = $sbr(Drive %id) $comval(sysinst,%id,Model) $+ , $iif($comval(sysinst,%id,Size),$bytes($v1).suf size $+ $chr(44)) $iif($comval(sysinst,%id,Partitions),$v1 partition(s) $+ $chr(44)) $comval(sysinst,%id,InterfaceType) interface }
  elseif (%cls == Win32_VideoController) { var %n = $sbr(Video controller %id) $comval(sysinst,%id,Caption) $+ $iif($comval(sysinst,%id,VideoProcessor),$chr(32) $nbr(Processor: $v1)) $+ , $+ $iif($comval(sysinst,%id,AdapterRAM),$chr(32) $bytes($v1).suf) $+ $iif($comval(sysinst,%id,CurrentHorizontalResolution),$+($chr(44) $v1,x,$comval(sysinst,%id,CurrentVerticalResolution),x,$comval(sysinst,%id,CurrentBitsPerPixel))) $+ $iif($comval(sysinst,%id,CurrentRefreshRate) isnum 0-,$chr(44) $v1 $+ Hz) $+ , driver version: $comval(sysinst,%id,DriverVersion) }
  elseif (%cls == Win32_NetworkAdapter) { var %n = $iif(s isin $3,$sbr(Network adapters)) $comval(sysinst,%id,Description) $+ $iif($3 != -e,$chr(44)) }
  elseif (%cls == Win32_OperatingSystem) {
    sysinfo.chkuptimerecord
    var %z = $comval(sysinst,%id,ServicePackMajorVersion),%n = $sbr(Operating system) $sysinfo.fixosdisp($comval(sysinst,%id,Caption)) $+ $iif(%z,$chr(32) $nbr(SP $+ %z)) $+ , version $comval(sysinst,%id,Version) $nbr($comval(sysinst,%id,NumberOfProcesses) processes running $+ $chr(44) $bytes($calc($comval(sysinst,%id,FreePhysicalMemory) *1024)).suf free RAM $+ $chr(44) installed $wtfconv($comval(sysinst,%id,InstallDate)).dur ago $+ $chr(44) $sduration($uptime(system,3)) uptime $+ $chr(44) $sduration($sysinfo.uptimerecord) uptime record)
  }
  elseif (%cls == Win32_SoundDevice) { var %n = $sbr(Sound card %id) $comval(sysinst,%id,Caption) $+ , brand: $comval(sysinst,%id,Manufacturer) }
  elseif (%cls == Win32_Printer) { var %h = $comval(sysinst,%id,HorizontalResolution),%v = $comval(sysinst,%id,VerticalResolution),%n = $sbr(Printer %id) $comval(sysinst,%id,Caption) on $comval(sysinst,%id,PortName) $nbr($iif(%h && %v,$+(%h,x,%v,dpi,$chr(44))) Status: $replacex($comval(sysinst,%id,PrinterStatus),1,Other,2,Unknown,3,Idle,4,Printing,5,Warmup,6,Stopped printing,7,Offline)) }
  
  if ($len(%sysinfo.say %n) > 400) {
    say %sysinfo.say
    set %sysinfo.say %n
  }
  else { set %sysinfo.say %sysinfo.say %n }
}

on *:active:*:{ if ($dialog(sysinfo)) { did $iif($nomsg,-b,-e) sysinfo 5,10 } }

alias wtfconv {
  tokenize 43 $1-
  var %x = $ctime($+($mid($1,7,2),.,$mid($1,5,2),.,$mid($1,1,4) $mid($1,9,2),:,$mid($1,11,2),:,$mid($1,13,2)))
  if (%x > $ctime) { %x = $ctime }
  if ($prop == dur) { return $sduration($calc($ctime - %x)) }
  elseif ($prop == both) { return $longtime(%x) $nbr($sduration($calc($ctime - %x)) ago) }
  else { return $longtime(%x) }
}

alias sysinfo.disk.total {
  var %r = 0,%i = $disk(0)
  while (%i) {
    if ($istok(fixed removable remote,$disk(%i).type,32)) {
      if ($1 == free) { inc %r $disk(%i).free }
      else { inc %r $disk(%i).size }
    }
    dec %i
  }
  return %r
}

alias sysinfo.uptimerecord {
  sysinfo.chkuptimerecord
  return %sysinfo.uptimerecord
}

alias sysinfo.chkuptimerecord { if (!%sysinfo.uptimerecord) || ($uptime(system,3) > %sysinfo.uptimerecord) { set %sysinfo.uptimerecord $v1 } }

; 
; End of file
; 
